// private endpointと、Private DNSゾーンへのAレコード登録(設計 §8)。

param name string
param location string
param tags object
param subnetId string
param targetResourceId string
@description('private link対象のサブリソース(blob、queue、vault、postgresqlServer)')
param groupId string
param privateDnsZoneId string

resource endpoint 'Microsoft.Network/privateEndpoints@2024-05-01' = {
  name: name
  location: location
  tags: tags
  properties: {
    subnet: { id: subnetId }
    privateLinkServiceConnections: [
      {
        name: name
        properties: {
          privateLinkServiceId: targetResourceId
          groupIds: [ groupId ]
        }
      }
    ]
  }
}

resource dnsGroup 'Microsoft.Network/privateEndpoints/privateDnsZoneGroups@2024-05-01' = {
  parent: endpoint
  name: 'default'
  properties: {
    privateDnsZoneConfigs: [
      { name: groupId, properties: { privateDnsZoneId: privateDnsZoneId } }
    ]
  }
}
